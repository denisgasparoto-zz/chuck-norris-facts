apply(plugin: "jacoco")

jacoco {
    toolVersion = '0.8.5'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoUnitTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            '**/*Generated.java',
            'build/**',
            '**/src/main/res/**',
            '**/AndroidManifest.xml',
            'android/**/*.*',
            '**/*Adapter*',
            '**/*ViewHolder*',
            '**/*Router*',
            '**/*Contract*',
            '**/*Activity*',
            '**/*Application*.*',
            '**/core/base/*',
            '**/core/di/*',
            '**/core/extensions/*',
            '**/core/network/*',
            '**/domain/model/*'
    ]

    def mainSrc = "${project.projectDir}/src/main/java"
    def javaDebugTree = fileTree(dir: "$buildDir/intermediates/classes/debug", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)

    classDirectories.setFrom(files([javaDebugTree], [kotlinDebugTree]))
    executionData.setFrom(files("$buildDir/jacoco/testDebugUnitTest.exec"))
    sourceDirectories.setFrom(files([mainSrc]))
}
